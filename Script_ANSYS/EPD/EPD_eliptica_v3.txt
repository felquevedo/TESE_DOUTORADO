!***********************************************************!
!** PROGRAMA EPD ELIPTICA		                  **!
!** Versão: 1.0						  **!
!**                                                       **!
!** Objetivo: calcula túnel estado plano de deformações   **!
!**                                                       **!
!** Programador: Felipe Quevedo                           **!
!** Situação   : em teste (01/09/2021)                    **!
!** Unidades   : MPa,m,s                                  **!
!**                                                       **!
!***********************************************************!
!
!***********************************************************!
! 1. Inicializando o Ansys                                  !
!***********************************************************!
FINISH 
/CLEAR,NOSTART
!
!***********************************************************!
! 2. Dados de entrada                                       !
!***********************************************************!
!
! Nessa seção define-se todos os dados de entrada necessarios
!
!***********************************************************!
! 2.1 Parâmetros geométricos                          	    !
!***********************************************************! 
Rhi		= 1		! [m] raio horizontal externo do túnel
Rvi		= 1		! [m] raio vertical externo do túnel
esp		= 0.1		! [m] espessura do revestimento
R1		= 10*Rvi	! [m] raio da região de refinamento da malha próxima do túnel
Lx		= 20*Rvi	! [m] dimensão da base do domínio
Ly		= 20*Rvi	! [m] dimensão da altura do domínio
!
!***********************************************************!
! 2.2 Modelos e parâmetros para o material do maciço        !
!***********************************************************! 
!
! Modelo do material do maciço
! matmacico = 1				! elastico do ANSYS
! matmacico = 2				! elastoplastico VM do ANSYS (BISO)
! matmacico = 22			! elastoplastico VM do ANSYS (MISO)
! matmacico = 23			! viscoplastico Perzyna VM do ANSYS
! matmacico = 4				! usermat3D_VM do ANSYS (BISO)
! matmacico = 5				! usermat3D_elastico
! matmacico = 6				! usermat3D_EP
! matmacico = 7				! usermat3D_VP
! matmacico = 8				! usermat3D_EPVP
!
matmacico	= 6			! Tipo de modelo escolhido
!
! Parâmetros de todos os modelos
E1 		= 1430			! [MPa] modulo elasticidade (1,2,22,23,4,5,6,7,8)
nu1 		= 0.498			! [adm] coeficiente de poisson (1,2,22,23,4,5,6,7,8)
!
! Parametros para o modelo 6 e 8
superficief	= 2			! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (6,8)
superficieg 	= 2			! função potencial: 1-DPI, 2-DPII, 3-DPIII (6,8)
fi	    	= 0			! [graus] angulo de atrito (0 - VM ou TR) (6,8)
psi	    	= 0			! [graus] angulo de dilatância (6,8)
c1	    	= 0.21*SQRT(3)/2 !4*SQRT(3)/2 !0.866025		! [MPa]	coesão inicial (6,8)
c2	    	= 0.56*SQRT(3)/2 !4*SQRT(3)/2 !1.0			! [MPa] coesão de pico (6,8)
c3	    	= 0.56*SQRT(3)/2 !0.56*SQRT(3)/2 !4*SQRT(3)/2 !0.866025		! [MPa] coesão residual (6,8)
eps1	    	= 0.024			! [adm] deformação equivalente limite da zona 1 (6,8)
eps2	    	= 0.024			! [adm] deformação equivalente limite da zona 2 (6,8)
eps3	    	= 0.024			! [adm] deformação equivalente limite da zona 3 (6,8)
Dalg		= 0			! 0 - módulo elastico, 1 - módulo algoritmíco
!
! Parametros para o modelo 7 e 8
superficiefvp 	= 2			! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (7,8)
superficiegvp 	= 2			! função potencial: 1-DPI, 2-DPII, 3-DPIII (7,8)
fivp          	= 0			! [graus] angulo de atrito (0 - VM ou TR)(7,8)
psivp         	= 0			! [graus] angulo de dilatância (7,8)
cvp 	      	= 1000 !3*SQRT(3)/2			! [MPa]	coesão inicial (7,8)
n1	      	= 1			! [adm] expoente do modelo de Perzyna (7,8)
eta           	= 4*10000	!1E7/(60*60*24)	! [dia] constante de viscosidade dinâmica (7,8)
f0     	      	= 1			! [MPa] valor de referência (7,8)
thetavp       	= 0			! forma de integração: 0 - totalmente explícito, 1 - semi-implicito (7,8)
!
! Parametros para o modelo 2, 22, 23, 4
yield	    	= 2*c1			! [MPa]	tensão de escoamento (2,22,23,4)
Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente (2,22,23,4)
youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente (2,22,23,4)
epst0		= 2*c1/E1		! [adm] deformação total no escoamento (22)
epst1		= 2*c2/E1 + eps1 	! [adm] deformação total na zona 1 (22)	
epst2		= 2*c2/E1 + eps2	! [adm] deformação total na zona 2 (22)	
epst3		= 2*c3/E1 + eps3	! [adm] deformação total na zona 3 (22)	
gamma	      	= ((2*cvp/f0)**n1)/eta 	! [dias^-1] coeficiente Gamma do modelo de Perzyna Ansys (23)
m  	      	= 1/n1			! [adm] coeficiente m do modelo de Perzyna do ANSYS (23)
!
!***********************************************************!
! 2.3 Condições de contorno				    !
!***********************************************************! 
!
! Pressão geostática
pi		= 2.5		! [MPa] pressão em Ri
pv		= 4.5		! [MPa] pressão vertical
ph		= 4.5		! [MPa] pressão horizontal
!
!***********************************************************!
! 2.4 Discretização do tempo nos passos			    !
!***********************************************************! 
!
vkbc		 = 1		! 0 - solicitação linearmente interpolada no passo, 1 - solicitação linearmente interpolada no primeiro subpasso
t1		 = 1		! [incremento de tempo ou partes inteiras da solicitação] do passo
dt 		 = t1/100	! [incremento de tempo ou incremento de partes inteiras da solicitação] a cada subpasso
!
!***********************************************************!
! 2.5 Discretização da malha 			    	    !
!***********************************************************! 
!
tipoelem	= 2		! 1 - PLANE182, 2 - PLANE183, 3 - PLANE42
nrev		= 1 		! [un] divisões na espessura do revestimento
nRi		= 10		! [un] divisões na interface entre o túnel e o maciço
nR1		= 15		! [un] divisões ao longo do raio da região de refinamento
mR1		= 15		! [%] taxa de crescimento de nr1
nL1		= 5		! [un] divisões na base fora da região de refinamento
mL1		= 1.2		! [%] taxa de crescimento de mx1
!
!***********************************************************!
! 2.6 Configurações da solução			    	    !
!***********************************************************! 
!
nr		= 2		! Newton-Raphson
!				! 	1 - INIT matriz de rigidez não é atualizada
!				! 	2 - FULL matriz de rigidez atualizada a cada iteração
!				! 	3 - UNSYM matriz de rigidez atualizada a cada iteração
!
psc		= 0		! 1 - Ativa, 0 - desativa a opção de paralelização SMP
nlg		= 0		! 1 - Ativa, 0 - desativa a não lineariedade geométrica
soleq		= 1		! 1 - calcula o equilibrio do maciço antes de iniciar
!
!***********************************************************!
! 2.7 Configurações do pós processamento	    	    !
!***********************************************************! 
!
! Parametros para pós-processamento
arqconv = 'results'	! Nome do arquivo de saida de resultados
!
! Salva os parâmetros em um arquivo de texto
PARSAV,ALL,parametros,txt		! salva os parâmetros da análise
!
!
!
!
!------------------FIM ENTRADA DE DADOS---------------------!
!
!
!
!
!***********************************************************!
! 3. Pré-Processamento                                      !
!***********************************************************!
/PREP7
!
!***********************************************************!
! 3.1 Alguns parâmetros calculados                          !
!***********************************************************!
nRv		= nRi/2	
nRh		= nRi/2
ratio		= Rvi/Rhi		
Lx1		= Lx-R1		
Ly1		= Ly-R1
!
!***********************************************************!
! 3.2 Configurando elemento finito                          !
!***********************************************************!
*IF,tipoelem,EQ,1,THEN
	ET,1,PLANE182
	KEYOPT,1,1,0
	KEYOPT,1,3,1
	KEYOPT,1,6,0
*ELSEIF,tipoelem,EQ,2,THEN
	ET,1,PLANE183
	KEYOPT,1,1,0
	KEYOPT,1,3,1
	KEYOPT,1,6,0
*ELSEIF,tipoelem,EQ,3,THEN
	ET,1,PLANE42
	KEYOPT,1,1,0
	KEYOPT,1,2,1
	KEYOPT,1,3,2
	KEYOPT,1,5,0
	KEYOPT,1,6,0
*ENDIF
!
!***********************************************************!
! 3.3 Material do maciço                                    !
!***********************************************************!
*IF,matmacico,eq,1,then
	! Definindo o material elastico do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
*ELSEIF,matmacico,eq,2,then
	! Definindo o material elastoplástico VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,youngt,,,,
*ELSEIF,matmacico,eq,22,then
	! Definindo o material elastoplástico VM do Ansys multilinear
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	!TB,PLASTIC,,,,MISO
	TB,MISO,1,1,5,0 
	TBTEMP,0
	TBPT,,epst0,2*c1
	TBPT,,epst1,2*c2
	TBPT,,epst2,2*c2 
	TBPT,,epst3,2*c3
	TBPT,,0.1,2*c3    
*ELSEIF,matmacico,eq,23,then
	! Definindo o material viscoplastico perzyna VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,2*cvp,0,,,,
	TB,RATE,1,1,2,1
	TBTEMP,0
	TBDATA,,m,gamma,,,,	
*ELSEIF,matmacico,eq,4,then
	! Definindo material usermat3D VM do Ansys
	TB,USER,1,2,5
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1,yield,youngt ! modelo, E, posn, sigy, H
	TB,STATE,1,,8
*ELSEIF,matmacico,eq,5,then
	! Definindo material usermat3D_elastico
	TB,USER,1,2,3
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TB,STATE,1,,8	
*ELSEIF,matmacico,eq,6,then
	! Definindo material usermat3D_EP
	TB,USER,1,2,14
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TB,STATE,1,,20	
*ELSEIF,matmacico,eq,7,then
	! Definindo material usermat3D_VP
	TB,USER,1,2,13
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficiefvp,superficiegvp
	TBDATA,6,fivp,psivp,
	TBDATA,8,cvp,n1,eta,f0,thetavp	
	TB,STATE,1,,20		
*ELSEIF,matmacico,eq,8,then
	! Definindo material usermat3D_EPVP
	TB,USER,1,2,23
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TBDATA,15,superficiefvp,superficiegvp
	TBDATA,17,fivp,psivp
	TBDATA,19,cvp
	TBDATA,20,n1,eta,f0
	TBDATA,23,thetavp	
	TB,STATE,1,,20	
*ENDIF
!
!***********************************************************!
! 3.4 Criando o modelo sólido	                            !
!***********************************************************!
!
! Criando pontos chaves
K,1,0,0,0
K,2,Rhi-esp,0,0
K,3,Rhi,0,0
K,4,R1,0,0
K,5,Lx,0,0
K,6,0,Rvi-esp,0
K,7,0,Rvi,0
K,8,0,R1,0
K,9,0,Ly,0
K,10,Lx,Ly
!
! criando linhas
L,1,2,nRh
L,2,3,nrev
L,3,4,nR1,mR1
L,4,5,nL1,mL1
LOCAL,11,1,,,,,,,ratio,
L,6,2,nRi
L,7,3,nRi
LOCAL,11,1,,,,,,,1,
L,8,4,nRi
LOCAL,11,0
L,9,10,nRi/2
L,1,6,nRv
L,6,7,nrev
L,7,8,nR1,mR1
L,8,9,nL1,mL1
L,5,10,nRi/2
!
! criando areas
LSEL,ALL
LSEL,S,LINE,,1
LSEL,A,LINE,,5
LSEL,A,LINE,,9
AL,ALL
!
LSEL,ALL
LSEL,S,LINE,,2
LSEL,A,LINE,,6
LSEL,A,LINE,,10
LSEL,A,LINE,,5
AL,ALL
!
LSEL,ALL
LSEL,S,LINE,,3
LSEL,A,LINE,,7
LSEL,A,LINE,,11
LSEL,A,LINE,,6
AL,ALL
!
LSEL,ALL
LSEL,S,LINE,,4
LSEL,A,LINE,,13
LSEL,A,LINE,,8
LSEL,A,LINE,,12
LSEL,A,LINE,,7
AL,ALL
!
!***********************************************************!
! 3.5 Aplicando condições de contorno                       !
!***********************************************************!
! Aplicação das tensões iniciais
ALLSEL,ALL
INISTATE, DEFINE,,,,,-pv,-ph,-ph,0,0,0		
!
ALLSEL,ALL
LSEL,S,LOC,Y,0,Ly,0
LSEL,R,LOC,X,0,0,0
DL,ALL, ,SYMM 
!
ALLSEL,ALL
LSEL,S,LOC,X,0,Lx,0
LSEL,R,LOC,Y,0,0,0
DL,ALL, ,SYMM 
!
ALLSEL,ALL
LSEL,S,LOC,X,0,Lx,0
LSEL,R,LOC,Y,Ly,Ly,0
SFL,ALL,PRESS,pv 
!
ALLSEL,ALL
LSEL,S,LOC,Y,0,Ly,0
LSEL,R,LOC,X,Lx,Lx,0
SFL,ALL,PRESS,ph 
!
ALLSEL,ALL
LSEL,S,LINE,,6
SFL,ALL,PRESS,pi
!
!***********************************************************!
! 3.6 Criando a malha			                    !
!***********************************************************!
ALLSEL,ALL
LSEL,S,LINE,,13
LSEL,A,LINE,,8
LCCAT,ALL
!
ALLSEL,ALL
LSEL,S,LINE,,1
LSEL,A,LINE,,9
LCCAT,ALL
!
ALLSEL,ALL
MSHAPE,0,2D
AMESH,ALL
!
!***********************************************************!
! 3.7 Criando grupos de elementos da região escavada        !
!***********************************************************!
!
! Criando grupos de elementos da escavação
ALLSEL,ALL
ASEL,S,AREA,,1
ESLA,S
CM,esc1,ELEM
!
! Criando grupos de elementos da região do revestimento
ALLSEL,ALL
ASEL,S,AREA,,2
ESLA,S
CM,rev1,ELEM
!
!***********************************************************!
! 4. Solução			   			    !
!***********************************************************!
/SOLU	
!
!***********************************************************!
! 4.1 Configuração da solução   			    !
!***********************************************************!
/SOL			
!
! Configurações iniciais
*IF,nlg,eq,0,then		
	NLGEOM,OFF
*ELSEIF,nlg,eq,1,then		
	NLGEOM,ON
*ENDIF
*IF,nr,eq,1,then		
	NROPT,INIT
*ELSEIF,nr,eq,2,then		
	NROPT,FULL
*ELSEIF,nr,eq,3,then		
	NROPT,UNSYM
*ENDIF	
*IF,psc,eq,0,then		
	PSCONTROL,ALL,OFF
*ELSEIF,nlg,eq,1,then		
	PSCONTROL,ALL,ON
*ENDIF
OUTRES,SVAR,ALL	
OUTRES,ALL,ALL		
ALLSEL,ALL
!
!***********************************************************!
! 4.2 Primeira solução 		   			    !
!***********************************************************!
ANTYPE,0,NEW			! 0 - Análise estática, New - nova
!
! Eliminando elementos da região do túnel
CMSEL,S,esc1,ELEM
CMSEL,A,rev1,ELEM	
EKILL,ALL
!
! Define o tempo
DELTIM,dt,dt,dt,OFF		! dtime incremento do tempo do passo, incremento mínimo, incremento máximo, chave (ON usa dtime do passo anterior se AUTOTS for ON, OFF - usa dtime no inicio de cada passo)	
TIME,t1
KBC,vkbc
!
! Resolve	
ESEL,S,LIVE
ESEL,ALL
SOLVE   
FINISH 
!
!***********************************************************!
! 5. Pós processamento                                      !
!***********************************************************!
!
!***********************************************************!
! 5.1 Alterando a cor dos elementos para pós processamento  !
!***********************************************************!
ALLSEL,ALL
ASEL,S,AREA,,1
ASEL,A,AREA,,3
ESLA,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
ASEL,S,AREA,,2
ASEL,A,AREA,,4
ESLA,S
/COLOR,ELEM,14,ALL
ALLSEL,ALL
! 
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15   
!
!***********************************************************!
! 5.2 Saida de gráficos                                     !
!***********************************************************!
/POST1
/PLOPTS,INFO,ON	! ativa informações 
SUBSET,LAST	! pega o último subpasso de resultados	
RSYS,1		! sistema cilindrico para os resultados
ESEL,S,LIVE	! só elementos ativos
!
! selecionando nós
ALLSEL,ALL 
NSEL,S,LOC,Y,0,0
NSEL,R,LOC,X,Rhi,Rhi
*GET,node1,NODE,0,NUM,MIN
!
ALLSEL,ALL 
NSEL,S,LOC,Y,0,0
NSEL,R,LOC,X,Lx,Lx
*GET,node2,NODE,0,NUM,MIN
!
! Criando o caminho
ALLSEL,ALL
PATH,caminho1,2,30,40,  
PPATH,1,node1
PPATH,2,node2
!
! Mapeando resultados no caminho
PDEF,ux,U,X,AVG
PDEF,sx,S,X,AVG 
PDEF,sy,S,Y,AVG 
PDEF,sz,S,Z,AVG 
PLPATH,ux
PLPATH,sx
PLPATH,sy
PLPATH,sz
PAGET,results,TABL 
!
! Saindo resultados em txt
*CREATE,ansuitmp
*CFOPEN,%arqconv%,'txt',' '
*VWRITE,'r','UX','SX','SY','SZ'
%14C %14C  %14C  %14C  %14C 
*VWRITE,results(1,1),results(1,5),results(1,6),results(1,7),results(1,8), , , , , ,   
(F10.4, '   ' , E10.4, '   ' , E10.4, '   ' , E10.4, '   ' , E10.4, '   ' , E10.4)  
*CFCLOS 
*END
/INPUT,ansuitmp 
/PLOPTS,INFO,on	
!

