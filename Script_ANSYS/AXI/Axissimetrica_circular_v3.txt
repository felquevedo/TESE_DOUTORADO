!***********************************************************!
!** SCRIPT TUNEL AXISSIMÉTRICO                            **!
!** Versão: 2021.3                                        **!
!**                                                       **!
!** Objetivo: faz análise da convergência de tuneis  	  **!
!**	      profundos com seção circular considerando   **!
!**	      modelo axissimétrico.                       **!
!**                                                       **!
!** Programador: Felipe Quevedo 		          **!
!** Inicio     : (05/06/2021)                             **!
!** Situação   : OK			                  **!
!** Unidades   : MPa,m,dia                                **!
!**                                                       **!
!***********************************************************!
!
!***********************************************************!
! 1. CONFIGURAÇÕES INICIAIS                                 !
!***********************************************************!
! Inicializando o ANSYS
FINISH 
/CLEAR,NOSTART
!
! Nome para os arquivos de saída
arqconv = 'D1-ES1-ER1-P1-C1-V1'
!
! Obs: nome do arquivo de texto com os dados da convergência
!  no final da construção e no final do tempo.
!
!***********************************************************!
! 2. DADOS DE ENTRADA PARA O PROBLEMA                       !
!***********************************************************!
!
! Nessa seção define-se todos os dados de entrada necessarios
!
!***********************************************************!
! 2.1 Parâmetros geométricos                          	    !
!***********************************************************! 
Ri		= 1			! [m] raio externo do túnel (entre a interface e o maciço)
esp		= 0.1			! [m] espessura do revestimento
R1		= 10*Ri			! [m] raio da região de refinamento da malha próxima do túnel
Lx		= 20*Ri			! [m] dimensão da base do domínio
Lp		= 1/3*Ri		! [m] tamanho do passo de escavação
np		= 39			! [un] numero de passos de escavação
npi 		= 3			! [un] numero de passos escavados na primeira escavação
Ly2		= 25			! [lp] dimensão do trecho não escavado
d0		= 0			! [lp] dimensão não suportada (multiplo do passo)
revestirultimod0 = 1			! 0 - não reveste ultimo d0+p, 1 - reveste ultimo d0+p
revesteface	= 1			! 0 - não reveste a ultima face, 1 - refeste a ultima face
!
!***********************************************************!
! 2.2 Modelos e parâmetros para o material do maciço        !
!***********************************************************! 
!
! Modelo do material do maciço
! matmacico = 1				! elastico do ANSYS
! matmacico = 2				! elastoplastico VM do ANSYS (BISO)
! matmacico = 22			! elastoplastico VM do ANSYS (MISO)
! matmacico = 23			! viscoplastico Perzyna VM do ANSYS
! matmacico = 4				! usermat3D_VM do ANSYS (BISO)
! matmacico = 5				! usermat3D_elastico
! matmacico = 6				! usermat3D_EP
! matmacico = 7				! usermat3D_VP
! matmacico = 8				! usermat3D_EPVP
!
matmacico	= 1			! Tipo de modelo escolhido
!
! Parâmetros de todos os modelos
E1 		= 1000			! [MPa] modulo elasticidade (1,2,22,23,4,5,6,7,8)
nu1 		= 0.498			! [adm] coeficiente de poisson (1,2,22,23,4,5,6,7,8)
!
! Parametros para o modelo 6 e 8
superficief	= 1			! funçaõ de escoamento: 1-VM, 2-DP, 3-TR, 4-MC (6,8)
superficieg 	= 1			! função potencial: 1-VM, 2-DP, 3-TR, 4-MC (6,8)
flag	    	= 1			! 1- DP coincide com as bordas externas MC ou VM circunscreve TR (6,8) 
fi	    	= 0			! [graus] angulo de atrito (0 - TR ou VM) (6,8)
psi	    	= fi			! [graus] angulo de dilatância (6,8)
c1	    	= 1.4/2			! [MPa]	coesão inicial (6,8)
c2	    	= 1.4/2			! [MPa] coesão de pico (6,8)
c3	    	= 1.4/2			! [MPa] coesão residual (6,8)
eps1	    	= 0.010			! [adm] deformação equivalente limite da zona 1 (6,8)
eps2	    	= 0.050			! [adm] deformação equivalente limite da zona 2 (6,8)
eps3	    	= 0.065			! [adm] deformação equivalente limite da zona 3 (6,8)
!
! Parametros para o modelo 7 e 8
superficiefvp 	= 2			! função de escoamento: 1- (MC ou TR), 2- (DP ou VM) (7,8)
superficiegvp 	= 2			! função potencial: 1- (MC ou TR), 2- (DP ou VM) (7,8)
flagvp	      	= 1			! 1- DP coincide com as bordas externas MC (7,8)
fivp          	= fi			! [graus] angulo de atrito (0 - TR ou VM)(7,8)
psivp         	= psi			! [graus] angulo de dilatância (7,8)
cvp 	      	= 1.21/2		! (18,1.4)[MPa]	coesão inicial (7,8)
n1	      	= 1			! [adm] expoente do modelo de Perzyna (7,8)
eta           	= 1E7/(60*60*24)	! [dia] constante de viscosidade dinâmica (7,8)
f0     	      	= 1			! [MPa] valor de referência (7,8)
thetavp       	= 1			! forma de integração: 0 - totalmente explícito, 1 - semi-implicito (7,8)
!
! Parametros para o modelo 2, 22, 23, 4
yield	    	= 2*c1			! [MPa]	tensão de escoamento (2,22,23,4)
Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente (2,22,23,4)
youngt      	= Ep*E1/(E1+Ep)	! [MPa] módulo de elastoplástico tangente (2,22,23,4)
epst0		= 2*c1/E1		! [adm] deformação total no escoamento (22)
epst1		= 2*c2/E1 + eps1 	! [adm] deformação total na zona 1 (22)	
epst2		= 2*c2/E1 + eps2	! [adm] deformação total na zona 2 (22)	
epst3		= 2*c3/E1 + eps3	! [adm] deformação total na zona 3 (22)	
gamma	      	= ((2*cvp/f0)**n1)/eta 	! [dias^-1] coeficiente Gamma do modelo de Perzyna Ansys (23)
m  	      	= 1/n1			! [adm] coeficiente m do modelo de Perzyna do ANSYS (23)
!
!***********************************************************!
! 2.3 Modelos e parâmetros para o material do revestimento  !
!***********************************************************! 
!
! Modelo do material do revestimento
! matrev = 0				! sem revestimento
! matrev = 1				! elastico do ANSYS
!
matrev		= 1			! Tipo de modelo escolhido
E2 		= 30000			! [MPa] modulo elasticidade (1)
nu2 		= 0.3			! [adm] coeficiente de poisson (1)
!
!***********************************************************!
! 2.4 Condições de contorno				    !
!***********************************************************! 
!
! Pressão hidrostática
ph		= 5			! [MPa] Pressão geostática hidrostática
!
!***********************************************************!
! 2.5 Discretização do tempo nos passos			    !
!***********************************************************! 
!
! Discretização do tempo durante os passos de escavação
v		= 1		! [m/d] velocidade da escavação
tp		= v/Lp		! [d] tempo do passo de escavação
dt		= 20		! [d] incremento de tempo inicial dentro de cada passo
dtmax		= dt		! [d] incremento máximo de tempo dentro de cada passo
!
! Discretização do tempo nos passos após a escavação
tp2		= 20		! [d] tempo de cada passo após a escavação do túnel
np2		= 1		! [un] número de passos após a escavação do túnel
dtp2		= dt		! [d] incremento de tempo inicial dentro de cada passo
dtp2max		= dtmax		! [d] incremento máximo de tempo dentro de cada passo
!
!***********************************************************!
! 2.6 Discretização da malha 			    	    !
!***********************************************************! 
!
! Parametros para discretização da malha
nesp		= 2 		! [un] divisões na espessura do revestimento
nre		= 10		! [un] divisões na interface entre o túnel e o maciço
nr1		= 15		! [un] divisões ao longo do raio da região de refinamento
mr1		= 15		! [%] taxa de crescimento de nr1
nx1		= 5		! [un] divisões na base fora da região de refinamento
mx1		= 1.2		! [%] taxa de crescimento de mx1
ny1		= np		! [un] numero de elementos no trecho escavado
ny2		= 8		! [un] numero de elementos no trecho não escavado
my2		= 5		! [%] taxa de crescimento de ny2
nri		= nre/2		! [un] divisões da parte interna da seção do túnel
!
!***********************************************************!
! 2.7 Configurações da solução			    	    !
!***********************************************************! 
!
nr		= 1		! Newton-Raphson (0 - INIT, 1 - FULL)
bi		= 0		! Tempo automático (0 - não, 1- sim)
soleq		= 1		! 1 - calcula o equilibrio do maciço antes de iniciar
!
!
!
!------------------FIM ENTRADA DE DADOS---------------------!
!
!
!
!
!***********************************************************!
! 3. Pré-Processamento                                      !
!***********************************************************!
/PREP7			! inicia módulo de pré-processamento
!
!***********************************************************!
! 3.1 Parâmetros calculados                                 !
!***********************************************************!	
ly1		= np*lp		! [m] dimensão do trecho escavado
d0		= d0*lp		! [m] comprimento não revestido
ly2		= ly2*lp	! [m] comprimento do trecho não escavado
Lx1		= Lx-R1		! [m] altura acima da região de refinamento
Ly 		= Ly1+Ly2	! [m] comprimento total do modelo
nesc		= np-npi+1	! [un] total de escavações
!
PARSAV,ALL,parametros,txt	! salva os parâmetros da análise
!
!***********************************************************!
! 3.2 Configurando elemento finito                          !
!***********************************************************!
ET,1,PLANE182 
KEYOPT,1,1,0
KEYOPT,1,3,1
KEYOPT,1,6,0
!
!***********************************************************!
! 3.3 Material do maciço                                    !
!***********************************************************!
*IF,matmacico,eq,1,then
	! Definindo o material elastico do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
*ELSEIF,matmacico,eq,2,then
	! Definindo o material elastoplástico VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,youngt,,,,
*ELSEIF,matmacico,eq,22,then
	! Definindo o material elastoplástico VM do Ansys multilinear
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	!TB,PLASTIC,,,,MISO
	TB,MISO,1,1,5,0 
	TBTEMP,0
	TBPT,,epst0,2*c1
	TBPT,,epst1,2*c2
	TBPT,,epst2,2*c2 
	TBPT,,epst3,2*c3
	TBPT,,0.1,2*c3    
*ELSEIF,matmacico,eq,23,then
	! Definindo o material viscoplastico perzyna VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,0,,,,
	TB,RATE,1,1,2,1
	TBTEMP,0
	TBDATA,,m,gamma,,,,	
*ELSEIF,matmacico,eq,4,then
	! Definindo material usermat3D VM do Ansys
	TB,USER,1,2,5
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1,yield,youngt ! modelo, E, posn, sigy, H
	TB,STATE,1,,8
*ELSEIF,matmacico,eq,5,then
	! Definindo material usermat3D_elastico
	TB,USER,1,2,3
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TB,STATE,1,,8	
*ELSEIF,matmacico,eq,6,then
	! Definindo material usermat3D_EP
	TB,USER,1,2,14
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,flag,fi,psi,
	TBDATA,9,c1,c2,c3,eps1,eps2,eps3
	TB,STATE,1,,20	
*ELSEIF,matmacico,eq,7,then
	! Definindo material usermat3D_VP
	TB,USER,1,2,13
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficiefvp,superficiegvp
	TBDATA,6,flagvp,fivp,psivp,
	TBDATA,9,cvp,n1,eta,f0,thetavp	
	TB,STATE,1,,20		
*ELSEIF,matmacico,eq,8,then
	! Definindo material usermat3D_EPVP
	TB,USER,1,2,24
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,flag,fi,psi,
	TBDATA,9,c1,c2,c3,eps1,eps2,eps3
	TBDATA,15,superficiefvp,superficiegvp
	TBDATA,17,flagvp,fivp,psivp
	TBDATA,20,cvp
	TBDATA,21,n1,eta,f0
	TBDATA,24,thetavp	
	TB,STATE,1,,17	
*ENDIF
!
!***********************************************************!
! 3.4 Material do revestimento                              !
!***********************************************************!
*IF,matrev,EQ,1,THEN
	*DO,i,2,nesc,1
		MPTEMP,1,0  				
		MPDATA,EX,i,,E2			
		MPDATA,PRXY,i,,nu2
	*ENDDO 
*ENDIF
!
!***********************************************************!
! 3.5 Modelo sólido                                         !
!***********************************************************!
! Criando pontos chaves
K,1,0,0,0
K,10,Ri-esp,0,0
K,9,Ri,0,0
K,8,R1,0,0
K,7,Lx,0,0
K,101,0,Ly1,0
K,110,Ri-esp,Ly1,0
K,109,Ri,Ly1,0
K,108,R1,Ly1,0
K,107,Lx,Ly1,0
K,201,0,Ly,0
K,210,Ri-esp,Ly,0
K,209,Ri,Ly,0
K,208,R1,Ly,0
K,207,Lx,Ly,0
!
! Criação das linhas e discretização da malha
L,1,10,nRi
L,10,9,nesp
L,9,8,nR1,mR1
L,8,7,nx1,mx1
L,101,110,nRi
L,110,109,nesp
L,109,108,nR1,mR1
L,108,107,nx1,mx1
L,201,210,nRi
L,210,209,nesp
L,209,208,nR1,mR1
L,208,207,nx1,mx1
L,1,101,ny1
L,10,110,ny1
L,9,109,ny1
L,8,108,ny1
L,7,107,ny1
L,101,201,ny2,my2
L,110,210,ny2,my2
L,109,209,ny2,my2
L,108,208,ny2,my2
L,107,207,ny2,my2
!
! Criaçãod das áreas
LSEL,S,LINE,,1
LSEL,A,LINE,,14
LSEL,A,LINE,,5
LSEL,A,LINE,,13
AL,ALL
CM,A1,AREA
!
LSEL,S,LINE,,2
LSEL,A,LINE,,15
LSEL,A,LINE,,6
LSEL,A,LINE,,14
AL,ALL
CMSEL,U,A1,AREA
CM,A2,AREA
!
LSEL,S,LINE,,3
LSEL,A,LINE,,16
LSEL,A,LINE,,7
LSEL,A,LINE,,15
AL,ALL
CMSEL,U,A2,AREA
CM,A3,AREA
!
LSEL,S,LINE,,4
LSEL,A,LINE,,17
LSEL,A,LINE,,8
LSEL,A,LINE,,16
AL,ALL
CMSEL,U,A3,AREA
CM,A4,AREA
!
LSEL,S,LINE,,5
LSEL,A,LINE,,19
LSEL,A,LINE,,9
LSEL,A,LINE,,18
AL,ALL
CMSEL,U,A4,AREA
CM,A5,AREA
!
LSEL,S,LINE,,6
LSEL,A,LINE,,20
LSEL,A,LINE,,10
LSEL,A,LINE,,19
AL,ALL
CMSEL,U,A5,AREA
CM,A6,AREA
!
LSEL,S,LINE,,7
LSEL,A,LINE,,21
LSEL,A,LINE,,11
LSEL,A,LINE,,20
AL,ALL
CMSEL,U,A6,AREA
CM,A7,AREA
!
LSEL,S,LINE,,8
LSEL,A,LINE,,22
LSEL,A,LINE,,12
LSEL,A,LINE,,21
AL,ALL
CMSEL,U,A7,AREA
CM,A8,AREA
!
!***********************************************************!
! 3.6 Atribuindo malha                                      !
!***********************************************************!
ALLSEL,ALL
MSHKEY,1
MSHAPE,0,2D
AMESH,ALL
!
!***********************************************************!
! 3.7 Condições de contorno                                 !
!***********************************************************!
! Aplicação das tensões iniciais
ALLSEL,ALL
INISTATE, DEFINE,,,,,-ph,-ph,-ph,0,0,0	
!
! Simetria em x
LSEL,ALL
LSEL,S,LINE,,1
LSEL,A,LINE,,2
LSEL,A,LINE,,3
LSEL,A,LINE,,4
DL,ALL, ,SYMM 
!
! Simetria em y
LSEL,ALL
LSEL,S,LINE,,13
LSEL,A,LINE,,18
DL,ALL, ,SYMM 
!
! Pressão no topo do modelo
LSEL,ALL
LSEL,S,LINE,,9
LSEL,A,LINE,,10
LSEL,A,LINE,,11
LSEL,A,LINE,,12
SFL,ALL,PRESS,ph
!
! Pressão na lateral direita do modelo
LSEL,ALL
LSEL,S,LINE,,22
LSEL,A,LINE,,17
SFL,ALL,PRESS,ph
!
!
!***********************************************************!
! 3.8 Criando elementos do revestimento                     !
!***********************************************************!
! Obs: esses elementos ficam sobrepostos ao do solo. Durante
!      a análise, conforme vai desligando os elementos do 
!      solo vai-se ligando os do resvestimento, se houver.
!
*IF,matrev,NE,0,THEN
	! Gerando elementos sobrepostos do revestimento
	ESEL,ALL
	ESEL,S,CENT,Y,0,ly1			
	ESEL,R,CENT,X,ri-esp,ri
	EGEN,2,0,all,all,1,1,,,,0,0,0
	ESEL,ALL
	NUMMRG,node, , , ,LOW				
*ENDIF
!
!***********************************************************!
! 3.9 Criando grupos de escavação-revestimento              !
!***********************************************************!
!
! Primeira escavação
i = 1
ESEL,ALL				
yi=0					
yf=npi*lp				
ESEL,S,CENT,Y,yi,yf			
ESEL,R,CENT,X,0,Ri
ESEL,R,MAT,,1			
CM,esc %i%,ELEM				
CMSEL,S,esc %i%,ELEM			
!
! Proximas escavações e resvestimentos
*DO,i,2,nesc,1					
	! criando grupos com os elementos escavados do passo i			
	yi=yf		
	yf=npi*lp+(i-1)*lp		
	ESEL,S,CENT,Y,yi,yf			
	ESEL,R,CENT,X,0,ri			
	CM,esc %i%,ELEM
	CMSEL,S,esc %i%,ELEM
	!
	*IF,matrev,NE,0,THEN
		! criando grupos com os elementos revestidos do passo i
		*IF,i,EQ,2,THEN		
			yi=0
		*ELSE
			yi=npi*lp+(i-2)*lp-(lp+d0)		
		*ENDIF
		yf=npi*lp+(i-1)*lp-(lp+d0)
		ESEL,S,CENT,Y,yi,yf			
		ESEL,R,CENT,X,ri-esp,ri
		ESEL,R,MAT,,2
		MPCHG,i,ALL
		CM,rev %i-1%,ELEM			
		CMSEL,S,rev %i-1%,ELEM						
	*ENDIF
	!
	! revestindo o ultimo trecho
	*IF,i,EQ,nesc,THEN
		*IF,matrev,NE,0,THEN
			*IF,revestirultimod0,EQ,1,THEN
				yi=Ly1-lp-d0
				yf=Ly1
				ESEL,S,CENT,Y,yi,yf	
				ESEL,R,CENT,X,ri-esp,ri	
				ESEL,R,MAT,,2
				MPCHG,i,ALL	
				CM,rev %i%,ELEM			
				CMSEL,S,rev %i%,ELEM
			*ENDIF
			*IF,revesteface,EQ,1,THEN
				yi=Ly1
				yf=Ly1+(lp)
				ESEL,ALL
				ESEL,S,CENT,Y,yi,yf
				ESEL,R,CENT,X,0,ri
				CM,soloface,ELEM
				CMSEL,S,soloface,ELEM
				!
				fi=Ly1
				ff=Ly1+(lp)
				ESEL,ALL
				ESEL,S,CENT,Y,yi,yf
				ESEL,R,CENT,X,0,ri
				EGEN,2,0,all,all,1,1,,,,0,0,0
				NUMMRG,node, , , ,LOW
				ESEL,R,MAT,,2
				MPCHG,i,ALL
				CM,revface,ELEM
				CMSEL,S,revface,ELEM	
			*ENDIF
		*ENDIF
	*ENDIF		
*ENDDO
!
! criando grupo de elementos escavados
ESEL,S,CENT,Y,0,ly1
ESEL,R,CENT,X,0,Ri
CM,esctotal,ELEM
!
!***********************************************************!
! 4. Solução                                                !
!***********************************************************!
/SOL			
!
! Configurações iniciais		
NLGEOM,1		
NROPT,nr		
PSCONTROL,ALL,OFF	
OUTRES,ESOL,LAST
SOLCONTROL,ON		
PRED,OFF		
ALLSEL,ALL	
!
!
! Apaga os elementos do revestimento
*IF,matrev,NE,0,THEN	
	ESEL,S,CENT,Y,0,Ly1		
	ESEL,R,CENT,X,ri-esp,ri
	ESEL,R,MAT,,2,nesc
	EKILL,ALL				
	ESEL,ALL
	*IF,revesteface,EQ,1,THEN
		CMSEL,S,revface,ELEM
		EKILL,ALL				
		ESEL,ALL	
	*ENDIF			
*ENDIF
!
! Verifica o equilibrio do maciço
*IF,soleq,EQ,1,THEN
	ANTYPE,0,NEW
	ESEL,ALL
	TIME,1
	DELTIM,dt,,dtmax			
	SOLVE								
	FINISH
	/POST1 
	RSYS,1    
	PLNSOL, U,SUM, 0,1.0  
	ESEL,ALL
*ENDIF
!
! Solução com as escavações
/SOL
*IF,matmacico,LT,1,AND,matrev,EQ,0,THEN
	!
	! Maciço elástico sem revestimento (não precisa escavar)
	ANTYPE,0,NEW	
	i=1
	CMSEL,S,esctotal,ELEM
	EKILL,ALL
	SAVE
	ESEL,S,LIVE
	SOLVE
	FINISH
*ELSE
	!
	! Necessário fazer vários passos de escavação
	ANTYPE,0,NEW	
	!
	! Primeiro passo de escavação
	i=1
	CMSEL,S,esc %i%,ELEM
	EKILL,ALL
	ESEL,S,LIVE
	EPLOT
	TIME,tp*i
	DELTIM,dt,,dtmax
	ESEL,ALL
	SOLVE
	SAVE
	FINISH
	/POST1   
	RSYS,1
	ESEL,S,LIVE 
	PLNSOL, U,SUM, 0,1.0    
	!
	! Próximos passos de escavação
	/SOL
	ANTYPE,0,RESTART	
	*DO,i,2,nesc,1
		CMSEL,S,esc %i%,ELEM
		EKILL,ALL
		ESEL,S,LIVE
		EPLOT
		!
		! Ativa o revestimento
		*IF,matrev,NE,0,THEN
			CMSEL,S,rev %i-1%,ELEM
			EALIVE,ALL
			*IF,revestirultimod0,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,rev %i%,ELEM
					EALIVE,ALL
				*ENDIF
			*ENDIF
			*IF,revesteface,EQ,1,THEN
				*IF,i,EQ,nesc,THEN
					CMSEL,S,revface,ELEM
					EALIVE,ALL
					CMSEL,S,soloface,ELEM
					EKILL,ALL
				*ENDIF
			*ENDIF
		*ENDIF
		!
		ESEL,S,LIVE
		EPLOT
		TIME,tp*i 
		DELTIM,dt,,dtmax
		OUTRES,ESOL,LAST
		ESEL,ALL
		SAVE
		SOLVE		
	*ENDDO
	!
	! Análise após a escavação (modelos viscosos)
	*IF,matmacico,GT,2,OR,matrev,GT,1,THEN	                    
		*DO,i,1,np2
			TIME,tp*(nesc)+i*tp2		
			DELTIM,dt,,dtmax							
			SOLVE						
			SAVE
		*ENDDO
	*ENDIF					
	FINISH
*ENDIF
!
!***********************************************************!
! 5 Pós-processamento                                       !
!***********************************************************!
!
!***********************************************************!
! 5.1 Alterando a cor dos elementos para pós processamento  !
!***********************************************************!
ALLSEL,ALL
ASEL,S,AREA,,1
ASEL,A,AREA,,5
ASEL,A,AREA,,3
ASEL,A,AREA,,7
ESLA,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
ASEL,S,AREA,,2
ASEL,A,AREA,,6
ASEL,A,AREA,,4
ASEL,A,AREA,,8
ESLA,S
/COLOR,ELEM,14,ALL
!
ALLSEL,ALL
EPLOT
! 
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15   
!
!***********************************************************!
! 5.2 Saida de gráficos                                     !
!***********************************************************!
/POST1 
RSYS,5		! sistema cilindrico para os resultados
ESEL,S,LIVE	! só elementos ativos
!
! numero de analises
*IF,matmacico,EQ,23,OR,matmacico,EQ,7,THEN
	nanalises=nesc+np2
*ELSE	
	*IF,matmacico,EQ,8,THEN
		nanalises=nesc+np2	
	*ELSE
		nanalises=nesc
	*ENDIF
*ENDIF
!
! selecionando nós
ALLSEL,ALL 
NSEL,S,LOC,Y,0,Ly
NSEL,R,LOC,X,0.999*Ri,1.001*Ri	
ESEL,S,LIVE	! só elementos ativos
!
! dimensionando arrays
*GET,ncount,NODE,,COUNT			! numero de nós selecionado
*GET,ntotal,NODE,,NUM,MAX		! número total de nós
*DIM,results,TABLE,ncount,2+nanalises	! dimensiona tabela de resultados
*DIM,coordy,ARRAY,ntotal		! dimensiona um array de 1 até ntotal 
*DIM,u_x,ARRAY,ntotal			! dimensiona um array ux de 1 até ntotal
*DIM,sx,ARRAY,ntotal			! dimensiona um array sx de 1 até ntotal
*DIM,n_sel,ARRAY,ntotal			! dimensiona um array dos nós selecionados
!
! Coloca lista de nós em ordem na primeira coluna do results
*VGET,results(1,1),NODE,,NLIST
!
! preenchendo o results com as coordenadas
*VGET,n_sel(1),NODE,1,NSEL		! marca com 1 os nós selecionados nas linhas de nsel
*VGET,coordy(1),NODE,1,LOC,y		! coloca as coordenadas y dos nós em ny
*VOPER,coordy(1),coordy(1),MULT,1	! multiplica as coordenadas por 1
*VMASK,n_sel(1)				! define nsel como sendo a máscara
*VFUN,results(1,2),COMP,coordy(1)	! coloca em results apenas os que tem máscara "true"
!
! preenchendo o results com a convergência
*DO,i,1,nanalises,1
	! identifica o passo
	*IF,i,EQ,nanalises,THEN
		SUBSET,LAST
	*ELSE
		*IF,matmacico,LT,1,AND,matrev,EQ,0,THEN
			SUBSET,LAST
		*ELSE
			SUBSET,,,,,,,i
		*ENDIF					
	*ENDIF
	!
	! Preenchendo o array com os deslocamentos radiais
	*VMASK,n_sel(1)				! define nsel como máscara
	*VGET,u_x(1),NODE,1,U,X			! coloca em ux os resultados nodais	
	*VOPER,u_x(1),u_x(1),MULT,-1/Ri*100	! Calcula a convergência
	*VMASK,n_sel(1)				! define nsel como sendo a máscara		
	*VFUN,results(1,2+i),COMP,u_x(1)	! coloca o ux nos resultados
*ENDDO
!
! ordena a tabela de acordo com a segunda coluna (das coordenadas)
*MOPER,ORDER,results,SORT,results(1,2)
!
! Coleta o maior valor das convergências
*VSCFUN,umaximofinal,max,results(1,2+nanalises)	
!
! formatando grafico de convergencia
/RGB,INDEX,100,100,100,0	
/RGB,INDEX,0,0,0,15 
/AXLAB,X,Y/Ri 			
/AXLAB,Y,U=-u(r=Ri)/Ri (%) 	
/XRANGE,0,ly/Ri			
/YRANGE,umaximofinal*1.2,0	
/GROPT,DIVY,20			
/GROPT,DIVX,10			
/GROPT,DIG1,3			
/GTHK,CURVE,1 			
/PLOPTS,INFO,off
!
! graficando na tela do Ansys
/ERASE
*DO,i,1,nanalises,1
	*IF,i,EQ,nanalises,THEN
		/COLOR,CURVE,MAGE,1
	*ELSEIF,i,EQ,nesc,THEN
		/COLOR,CURVE,YELL,1
	*ELSE
		/COLOR,CURVE,LGRA,1
	*ENDIF
	*VPLOT,results(1,2),results(1,2+i)
	/NOERASE
*ENDDO
/ERASE

*CREATE,ansuitmp
*MWRITE,results,convergencias,txt
(100(E10.4,3X)) 
*END
/INPUT,ansuitmp





