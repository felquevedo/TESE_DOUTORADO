!***********************************************************!
!** SCRIPT TUNEL 3D		                          **!
!** Versão: 2021.3                                        **!
!**                                                       **!
!** Objetivo: faz análise da convergência de tuneis  	  **!
!**	      profundos com seção eliptica considerando   **!
!**	      modelo 3D.	                          **!
!**                                                       **!
!** Programador: Felipe Quevedo 		          **!
!** Inicio     : (05/11/2021)                             **!
!** Situação   : OK			                  **!
!** Unidades   : MPa,m,dia                                **!
!**                                                       **!
!***********************************************************!
!
!***********************************************************!
! 1. CONFIGURAÇÕES INICIAIS                                 !
!***********************************************************!
! Inicializando o ANSYS
FINISH 
/CLEAR,NOSTART
!
!***********************************************************!
! 2. DADOS DE ENTRADA PARA O PROBLEMA                       !
!***********************************************************!
!
! Nessa seção define-se todos os dados de entrada necessarios
!
!***********************************************************!
! 2.1 Parâmetros geométricos                          	    !
!***********************************************************! 
!
! Parametros geométricos
Rhi		= 2		! [m] raio horizontal externo do túnel (entre a interface e o maciço)
Rvi		= 1		! [m] raio vertical externo do túnel
esp		= 0.1		! [m] espessura do revestimento
R1		= 10		! [Rvi] raio da região de refinamento da malha próxima do túnel
Lx		= 20		! [Rvi] dimensão da base do domínio
Ly		= 20		! [Rvi] dimensão da altura do domínio
Lz2		= 10		! [Rvi] dimensão do trecho não escavado
Lp		= 1/3*Rvi	! [m] tamanho do passo de escavação
np		= 38		! [un] numero de passos de escavação
npi		= 3		! [un] numero de passos escavados na primeira escavação
d0		= 0		! [lp] dimensão não suportada (multiplo do passo)
revested0 	= 1		! 0 - não reveste ultimo d0+Lp, 1 - reveste ultimo d0+Lp
revesteface	= 1		! 0 - não reveste a ultima face, 1 - reveste a ultima face
!
!***********************************************************!
! 2.2 Modelos e parâmetros para o material do maciço        !
!***********************************************************! 
!
! Modelo do material do maciço
! matmacico = 1				! elastico do ANSYS
! matmacico = 2				! elastoplastico VM do ANSYS (BISO)
! matmacico = 22			! elastoplastico VM do ANSYS (MISO)
! matmacico = 23			! viscoplastico Perzyna VM do ANSYS
! matmacico = 4				! usermat3D_VM do ANSYS (BISO)
! matmacico = 5				! usermat3D_elastico
! matmacico = 6				! usermat3D_EP
! matmacico = 7				! usermat3D_VP
! matmacico = 8				! usermat3D_EPVP
!
matmacico	= 8			! Tipo de modelo escolhido
!
! Parâmetros de todos os modelos
E1 		= 1500			! [MPa] modulo elasticidade (1,2,22,23,4,5,6,7,8)
nu1 		= 0.498			! [adm] coeficiente de poisson (1,2,22,23,4,5,6,7,8)
!
! Parametros para o modelo 6 e 8
superficief	= 2			! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (6,8)
superficieg 	= 2			! função potencial: 1-DPI, 2-DPII, 3-DPIII (6,8)
fi	    	= 15			! [graus] angulo de atrito (0 - VM ou TR) (6,8)
psi	    	= 0			! [graus] angulo de dilatância (6,8)
c1	    	= 4*SQRT(3)/2 !0.866025		! [MPa]	coesão inicial (6,8)
c2	    	= 4*SQRT(3)/2 !1.0			! [MPa] coesão de pico (6,8)
c3	    	= 4*SQRT(3)/2 !0.866025		! [MPa] coesão residual (6,8)
eps1	    	= 0.010			! [adm] deformação equivalente limite da zona 1 (6,8)
eps2	    	= 0.020			! [adm] deformação equivalente limite da zona 2 (6,8)
eps3	    	= 0.025			! [adm] deformação equivalente limite da zona 3 (6,8)
Dalg		= 0			! 0 - módulo elastico, 1 - módulo algoritmíco
!
! Parametros para o modelo 7 e 8
superficiefvp 	= 2			! função de escoamento: 1-DPI, 2-DPII, 3-DPIII (7,8)
superficiegvp 	= 2			! função potencial: 1-DPI, 2-DPII, 3-DPIII (7,8)
fivp          	= 0			! [graus] angulo de atrito (0 - VM ou TR)(7,8)
psivp         	= 0			! [graus] angulo de dilatância (7,8)
cvp 	      	= 3*SQRT(3)/2			! [MPa]	coesão inicial (7,8)
n1	      	= 1			! [adm] expoente do modelo de Perzyna (7,8)
eta           	= 4*10000	!1E7/(60*60*24)	! [dia] constante de viscosidade dinâmica (7,8)
f0     	      	= 1			! [MPa] valor de referência (7,8)
thetavp       	= 0			! forma de integração: 0 - totalmente explícito, 1 - semi-implicito (7,8)
!
! Parametros para o modelo 2, 22, 23, 4
yield	    	= 2*c1			! [MPa]	tensão de escoamento (2,22,23,4)
Ep 		= 2*(c2-c1)/(eps1)	! [MPa] módulo plástico tangente (2,22,23,4)
youngt      	= Ep*E1/(E1+Ep)		! [MPa] módulo de elastoplástico tangente (2,22,23,4)
epst0		= 2*c1/E1		! [adm] deformação total no escoamento (22)
epst1		= 2*c2/E1 + eps1 	! [adm] deformação total na zona 1 (22)	
epst2		= 2*c2/E1 + eps2	! [adm] deformação total na zona 2 (22)	
epst3		= 2*c3/E1 + eps3	! [adm] deformação total na zona 3 (22)	
gamma	      	= ((2*cvp/f0)**n1)/eta 	! [dias^-1] coeficiente Gamma do modelo de Perzyna Ansys (23)
m  	      	= 1/n1			! [adm] coeficiente m do modelo de Perzyna do ANSYS (23)
!
!***********************************************************!
! 2.3 Modelos e parâmetros para o material do revestimento  !
!***********************************************************! 
!
! Modelo do material do revestimento
! matrev = 0				! sem revestimento
! matrev = 1				! elastico do ANSYS
!
matrev		= 0			! Tipo de modelo escolhido
E2 		= 30000			! [MPa] modulo elasticidade (1)
nu2 		= 0.3			! [adm] coeficiente de poisson (1)
!
!***********************************************************!
! 2.4 Condições de contorno				    !
!***********************************************************! 
!
! Pressão geostática
ph		= 9			! [MPa] Pressão geostática normal ao eixo do túnel
pv		= 9			! [MPa] Pressão geostática radial
!
!***********************************************************!
! 2.5 Discretização do tempo nos passos			    !
!***********************************************************! 
!
! Discretização do tempo durante os passos de escavação
v		= 10		! [m/d] velocidade da escavação
tp		= Lp/v		! [d] tempo do passo de escavação
dt		= 0.5*tp	! [d] incremento de tempo inicial dentro de cada passo
dtmax		= dt		! [d] incremento máximo de tempo dentro de cada passo
!
! Discretização do tempo nos passos após a escavação
tp2		= 50		! 1[d] tempo de cada passo após a escavação do túnel
np2		= 200		! 10[un] número de passos após a escavação do túnel
dtp2		= 0.5*tp2	! 0.5[d] incremento de tempo inicial dentro de cada passo
dtp2max		= dtp2		! [d] incremento máximo de tempo dentro de cada passo
!
!***********************************************************!
! 2.6 Discretização da malha 			    	    !
!***********************************************************! 
!
! Parametros para discretização da malha
tipoelem	= 0		! 0 - SOLID185, 1 - SOLID186
nrev		= 2 		! [un] divisões na espessura do revestimento
nLp		= 1		! [un] numero de elementos no passo escavado
nRi		= 10		! [un] divisões na interface entre o túnel e o maciço
nR1		= 15		! [un] divisões ao longo do raio da região de refinamento
mR1		= 15		! [%] taxa de crescimento de nR1
nL1		= 5		! [un] divisões na base e altura fora da região de refinamento
mL1		= 1.2		! [%] taxa de crescimento de mL1
nLz2		= 15		! [un] numero de elementos no trecho não escavado
mLz2		= 5		! [%] taxa de crescimento de nLy2
!
!***********************************************************!
! 2.7 Configurações da solução			    	    !
!***********************************************************! 
!
nr		= 3		! Newton-Raphson
!				! 	1 - INIT matriz de rigidez não é atualizada
!				! 	2 - FULL matriz de rigidez atualizada a cada iteração
!				! 	3 - UNSYM matriz de rigidez atualizada a cada iteração
!
psc		= 0		! 1 - Ativa, 0 - desativa a opção de paralelização SMP
nlg		= 0		! 1 - Ativa, 0 - desativa a não lineariedade geométrica
soleq		= 1		! 1 - calcula o equilibrio do maciço antes de iniciar
!
!
!
!
!------------------FIM ENTRADA DE DADOS---------------------!
!
!
!
!
!***********************************************************!
! 3. Pré-Processamento                                      !
!***********************************************************!
/PREP7			! inicia módulo de pré-processamento
!
!***********************************************************!
! 3.1 Parâmetros calculados                                 !
!***********************************************************!	
R1		= R1*Rvi	! [m] raio da região de refinamento da malha próxima do túnel
Lx		= Lx*Rvi	! [m] dimensão da base do domínio
Lz1		= np*lp		! [m] dimensão do trecho escavado
nLz1		= np*nlp	! [un] numero de elementos no trecho escavado
nRv		= nRi/2		! [un] divisões na vertical da parte interna da seção do túnel
nRh 		= nRi/2		! [un] divisões na horizontal da parte interna da seção do túnel
d0		= d0*lp		! [m] comprimento não revestido
Lz2		= Lz2*Rvi	! [m] comprimento do trecho não escavado
Lx1		= Lx-R1		! [m] altura acima da região de refinamento
Ly1		= Ly-R1		! [m] comprimento além da região de refinamento
Lz 		= Lz1+Lz2	! [m] comprimento total do modelo
nesc		= np-npi+1	! [un] total de escavações
ratio		= Rvi/Rhi	! [adm] razão entre os raios da elipse	
!
!***********************************************************!
! 3.2 Configurando elemento finito                          !
!***********************************************************!
*IF,tipoelem,EQ,0,THEN
	ET,1,SOLID185  
	KEYOPT,1,2,0
	KEYOPT,1,3,0
	KEYOPT,1,6,0
	KEYOPT,1,8,0
*ELSEIF,tipoelem,EQ,1,THEN
	ET,1,SOLID186  
	KEYOPT,1,2,0
	KEYOPT,1,3,0
	KEYOPT,1,6,0
	KEYOPT,1,8,0
*ENDIF
!
!***********************************************************!
! 3.3 Material do maciço                                    !
!***********************************************************!
*IF,matmacico,eq,1,then
	! Definindo o material elastico do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
*ELSEIF,matmacico,eq,2,then
	! Definindo o material elastoplástico VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,yield,youngt,,,,
*ELSEIF,matmacico,eq,22,then
	! Definindo o material elastoplástico VM do Ansys multilinear
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1 	
	!TB,PLASTIC,,,,MISO
	TB,MISO,1,1,5,0 
	TBTEMP,0
	TBPT,,epst0,2*c1
	TBPT,,epst1,2*c2
	TBPT,,epst2,2*c2 
	TBPT,,epst3,2*c3
	TBPT,,0.1,2*c3    
*ELSEIF,matmacico,eq,23,then
	! Definindo o material viscoplastico perzyna VM do Ansys
	MPTEMP,1,0  		
	MPDATA,EX,1,,E1	
	MPDATA,PRXY,1,,nu1	
	TB,BISO,1,1,2,  
	TBTEMP,0
	TBDATA,,2*cvp,0,,,,
	TB,RATE,1,1,2,1
	TBTEMP,0
	TBDATA,,m,gamma,,,,	
*ELSEIF,matmacico,eq,4,then
	! Definindo material usermat3D VM do Ansys
	TB,USER,1,2,5
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1,yield,youngt ! modelo, E, posn, sigy, H
	TB,STATE,1,,8
*ELSEIF,matmacico,eq,5,then
	! Definindo material usermat3D_elastico
	TB,USER,1,2,3
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TB,STATE,1,,8	
*ELSEIF,matmacico,eq,6,then
	! Definindo material usermat3D_EP
	TB,USER,1,2,14
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TB,STATE,1,,20	
*ELSEIF,matmacico,eq,7,then
	! Definindo material usermat3D_VP
	TB,USER,1,2,13
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficiefvp,superficiegvp
	TBDATA,6,fivp,psivp,
	TBDATA,8,cvp,n1,eta,f0,thetavp	
	TB,STATE,1,,20		
*ELSEIF,matmacico,eq,8,then
	! Definindo material usermat3D_EPVP
	TB,USER,1,2,23
	TBTEMP,1.0 		
	TBDATA,1,matmacico,E1,nu1
	TBDATA,4,superficief,superficieg
	TBDATA,6,fi,psi,
	TBDATA,8,c1,c2,c3,eps1,eps2,eps3
	TBDATA,14,Dalg
	TBDATA,15,superficiefvp,superficiegvp
	TBDATA,17,fivp,psivp
	TBDATA,19,cvp
	TBDATA,20,n1,eta,f0
	TBDATA,23,thetavp	
	TB,STATE,1,,18	
*ENDIF
!
!***********************************************************!
! 3.4 Material do revestimento                              !
!***********************************************************!
*IF,matrev,EQ,1,THEN
	*DO,i,2,nesc,1
		MPTEMP,1,0  				
		MPDATA,EX,i,,E2			
		MPDATA,PRXY,i,,nu2
	*ENDDO 
*ENDIF
!
!***********************************************************!
! 3.5 Modelo sólido                                         !
!***********************************************************!
! Criando pontos chaves
K,1,0,0,0
K,2,Rhi-esp,0,0
K,3,Rhi,0,0
K,4,R1,0,0
K,5,Lx,0,0
K,6,Lx,Ly,0
K,7,0,Ly,0
K,8,0,R1,0
K,9,0,Rvi,0
K,10,0,Rvi-esp,0
!
! criando linhas
L,1,2
L,2,3
L,3,4
L,4,5
L,5,6
L,7,6
L,1,10
L,10,9
L,9,8
L,8,7
LOCAL,11,1,,,,,,,1,
L,8,4
LOCAL,11,1,,,,,,,ratio,
L,2,10
L,9,3
LOCAL,11,0
NUMCMP,ALL
!
! criando grupo de linhas
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0,Rhi-esp
LSEL,R,LOC,Y,0
CM,L1,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Rhi-esp,Rhi
LSEL,R,LOC,Y,0
CM,L2,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Rhi,R1
LSEL,R,LOC,Y,0
CM,L3,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,R1,Lx
LSEL,R,LOC,Y,0
CM,L4,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Lx
LSEL,R,LOC,Y,0,Ly
CM,L5,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0,Lx
LSEL,R,LOC,Y,Ly
CM,L6,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,R1,Ly
CM,L7,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,Rvi,R1
CM,L8,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,Rvi-esp,Rvi
CM,L9,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,0,Rvi-esp
CM,L10,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,ratio,
LSEL,S,LOC,X,Rhi
LSEL,R,LOC,Y,0,90
CM,L12,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,ratio,
LSEL,S,LOC,X,0,Rhi
LSEL,R,LOC,Y,0.01,89.9
CMSEL,U,L12,LINE
CM,L11,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,1,
LSEL,S,LOC,X,R1
LSEL,R,LOC,Y,0,90
CM,L13,line
!
! criando areas
CMSEL,S,L1,LINE
CMSEL,A,L11,LINE
CMSEL,A,L10,LINE
AL,ALL
!
CMSEL,S,L2,LINE
CMSEL,A,L12,LINE
CMSEL,A,L9,LINE
CMSEL,A,L11,LINE
AL,ALL
!
CMSEL,S,L3,LINE
CMSEL,A,L13,LINE
CMSEL,A,L8,LINE
CMSEL,A,L12,LINE
AL,ALL
!
CMSEL,S,L4,LINE
CMSEL,A,L5,LINE
CMSEL,A,L6,LINE
CMSEL,A,L7,LINE
CMSEL,A,L13,LINE
AL,ALL
!
! definindo volumes
VOFFST,1,-Lz
CM,V1,VOLUME
VOFFST,2,-Lz
CMSEL,U,V1
CM,V2,VOLUME
VOFFST,3,-Lz
CMSEL,U,V2
CM,V3,VOLUME
VOFFST,4,-Lz
CMSEL,U,V3
CM,V4,VOLUME
!
! dividindo o volume
ALLSEL,ALL
WPOFFS,0,0,-Lz1
VSBW,ALL
WPOFFS,0,0,Lz1
!
!
! eliminando geometrias duplicadas
AOVLAP,ALL
NUMMRG,KP, , , ,LOW
NUMCMP,KP
NUMCMP,LINE
NUMCMP,AREA
NUMCMP,VOLU
!
! adicionando as novas linhas para divisoes
fol = 0.01	! folga para selecao de linhas
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,fol,Rhi-esp-fol
LSEL,R,LOC,Y,0
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L1,LINE
CM,L1,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Rhi-esp+fol,Rhi-fol
LSEL,R,LOC,Y,0
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L2,LINE
CM,L2,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Rhi+esp,R1-esp
LSEL,R,LOC,Y,0
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L3,LINE
CM,L3,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,R1+fol,Lx-fol
LSEL,R,LOC,Y,0
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L4,LINE
CM,L4,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,Lx
LSEL,R,LOC,Y,0+fol,Ly-fol
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L5,LINE
CM,L5,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0+fol,Lx-fol
LSEL,R,LOC,Y,Ly
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L6,LINE
CM,L6,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,R1+fol,Ly-fol
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L7,LINE
CM,L7,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,Rvi+fol,R1-fol
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L8,LINE
CM,L8,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,Rvi-esp+fol,Rvi-fol
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L9,LINE
CM,L9,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,0+fol,Rvi-esp-fol
LSEL,R,LOC,Z,-lz1,-lz
CMSEL,A,L10,LINE
CM,L10,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0,lx
LSEL,R,LOC,Y,0,ly
LSEL,R,LOC,Z,0-fol,-lz1+fol
CM,L101,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0,lx
LSEL,R,LOC,Y,0,ly
LSEL,R,LOC,Z,-lz1-fol,-lz+fol
CM,L102,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,ratio,
LSEL,S,LOC,X,Rhi
LSEL,R,LOC,Y,0+fol,90-fol
LSEL,R,LOC,Z,0,-lz
CM,L12,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,ratio,
LSEL,S,LOC,X,0,Rhi
LSEL,R,LOC,Y,0+fol,90-fol
LSEL,R,LOC,Z,0,-lz
CMSEL,U,L12,LINE
CM,L11,line
!
ALLSEL,ALL
LOCAL,11,1,,,,,,,1,
LSEL,S,LOC,X,R1
LSEL,R,LOC,Y,0+fol,90-fol
LSEL,R,LOC,Z,0,-lz
CM,L13,line
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,Rvi+fol,R1-fol
LSEL,R,LOC,Z,-lz
LREVERSE,ALL
!
ALLSEL,ALL
LOCAL,11,0,,,,,,,1,
LSEL,S,LOC,X,0
LSEL,R,LOC,Y,R1+fol,Ly-fol
LSEL,R,LOC,Z,-lz
LREVERSE,ALL
!
! definindo espaçamentos da malha
ALLSEL,ALL
LESIZE,L1, , ,nRh,  , , , ,1
LESIZE,L2, , ,nrev,  , , , ,1
LESIZE,L3, , ,nR1,mR1, , , ,1
LESIZE,L4, , ,nL1,mL1, , , ,1
LESIZE,L5, , ,nRi/2, , , , ,1
LESIZE,L6, , ,nRi/2, , , , ,1
LESIZE,L7, , ,nL1,mL1, , , ,1
LESIZE,L8, , ,nR1,mR1, , , ,1
LESIZE,L9, , ,nrev, , , , ,1
LESIZE,L10, , ,nRv, , , , ,1
LESIZE,L11, , ,nRi, , , , ,1
LESIZE,L12, , ,nRi, , , , ,1
LESIZE,L13, , ,nRi, , , , ,1
LESIZE,L101, , ,np, , , , ,1
LESIZE,L102, , ,nLz2,1/mLz2, , , ,1
!
! criando grupo de areas da fronteira
ALLSEL,ALL
ASEL,S,LOC,Z,0
CM,AF1,AREA
!
ALLSEL,ALL
ASEL,S,LOC,Z,-Lz
CM,AF2,AREA
!
ALLSEL,ALL
ASEL,S,LOC,X,0
CM,AF3,AREA
!
ALLSEL,ALL
ASEL,S,LOC,X,Lx
ASEL,R,LOC,Z,0,-Lz1
CM,AF41,AREA
ALLSEL,ALL
ASEL,S,LOC,X,Lx
ASEL,R,LOC,Z,-lz1,-Lz
CM,AF42,AREA
ALLSEL,ALL
CMSEL,S,AF41,AREA
CMSEL,A,AF42,AREA
CM,AF4,AREA
!
ALLSEL,ALL
ASEL,S,LOC,Y,0
CM,AF5,AREA
!
ALLSEL,ALL
ASEL,S,LOC,Y,Ly
ASEL,R,LOC,Z,0,-Lz1
CM,AF61,AREA
ALLSEL,ALL
ASEL,S,LOC,Y,Ly
ASEL,R,LOC,Z,-lz1,-Lz
CM,AF62,AREA
ALLSEL,ALL
CMSEL,S,AF61,AREA
CMSEL,A,AF62,AREA
CM,AF6,AREA
!
ALLSEL,ALL
CMSEL,S,AF41,AREA
CMSEL,A,AF61,AREA
ACCAT,ALL
!
ALLSEL,ALL
CMSEL,S,AF42,AREA
CMSEL,A,AF62,AREA
ACCAT,ALL
!
! criando a malha
ALLSEL,ALL
!MSHAPE,0,3D
VMESH,ALL
!
! Colorindo elementos
ALLSEL,ALL
VSEL,S,VOLU,,1
ESLV,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,2
ESLV,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,4
ESLV,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,6
ESLV,S
/COLOR,ELEM,13,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,3
ESLV,S
/COLOR,ELEM,14,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,5
ESLV,S
/COLOR,ELEM,14,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,7
ESLV,S
/COLOR,ELEM,14,ALL
!
ALLSEL,ALL
VSEL,S,VOLU,,8
ESLV,S
/COLOR,ELEM,14,ALL
!
! visao ISO
/VIEW,1,,,1 
/ANG,1  
/ANG, 1 ,35,YS,1 
/ANG, 1 ,15,XS,1
/AUTO,1 
/REP,FAST
!
! visao detalhe
/ANG, 1 ,-15,XS,1 
/FOC, 1 ,,-0.05*16,,1 
/DIST, 1 ,0.948683/5,1 
/FOC, 1 ,,14*0.050000,,1   
/FOC, 1 ,-0.050000*18,,,1
/ANG, 1 ,1*5,XS,1 
/REP,FAST
